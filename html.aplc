:Class html
    (⎕IO ⎕ML)←0 1

    :Field id←''
    :Field hr
    :Field stop←0
    :Field game
    :Field html
    :Field js

    :Field Shared ReadOnly LOAD←18<⍎⊃'.'(≠⊆⊢)1⊃#⎕WG'APLVersion'
    :Field Shared ReadOnly HTML←'./index.html'
    :Field Shared ReadOnly JS←'./index.js'
    Read←(⊃∘⎕NGET)

    ∇   make1 arg
        :Access Public
        :Implements Constructor
        game←arg
        html←Read HTML
        js  ←Read JS
    ∇ 

    ∇   OnUpgrade arg
        id←2⊃arg
    ∇

    ∇   init;json
        :If stop≡1 ⋄ :Return  ⋄ :EndIf
        json←⎕NS'' ⋄ json.k←0 ⋄ json.(h w)←game.f
        {}hr.WebSocketSend id (1 ⎕JSON json) 1 1
    ∇

    ∇   Send arg;json
        :If stop≡1 ⋄ :Return  ⋄ :EndIf
        json←⎕NS'' ⋄ json.k←1 ⋄ json.(v t)←arg ⋄ json.f←,game.field
        {}hr.WebSocketSend id (1 ⎕JSON json) 1 1
    ∇

    ∇   r←Text v;cnt
        :Select v
        :Case 0
            cnt←+/'?'=,game.field
            r←1('Mines: ',⍕game.m-cnt) 
        :Case ¯1
            r←0('Boom')
        :Case 1
            r←0('Win')
        :EndSelect
    ∇

    ∇   OnRecv arg;k;p;r
        (k p)←⎕JSON 3⊃arg
        :If k≡2
            r←game.(Init(f m))
        :Else
            r←k game.Game game.f⊤p
        :EndIf
        Send Text r
    ∇

    ∇   OnClose arg
        stop←1
    ∇

    ∇   OnLoad arg;r
        hr.ExecuteJavaScript js
    ∇

    ∇   makeWindow;ev;pa;msg
        :If id≢'' ⋄ 'Reload the window'⎕SIGNAL 16 ⋄ :EndIf
        ev←('WebSocketUpgrade' 'OnUpgrade')('WebSocketReceive' 'OnRecv')('Close' 'OnClose')
        :If LOAD
            ev,←⊂'LoadEnd' 'OnLoad'
        :EndIf
        pa←('Event',⍥⊆ev)('HTML'html),{⍵(⎕SE⎕WG⍵)}¨'Coord' 'Size'
        {_←'hr'⎕WC'HTMLRenderer',⍥⊆⍵ ⋄ ⎕DQ'hr'}&pa
        :If ~LOAD
            OnLoad 0
        :EndIf
        :While (id≡'')∧stop≢1 ⋄ ⎕DL 0.01 ⋄ :EndWhile
    ∇

    ∇   loop
        :Access Public
        makeWindow
        init
        Send Text 0
    ∇

:EndClass
